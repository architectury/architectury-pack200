plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "java-gradle-plugin"
    id "org.cadixdev.licenser" version "0.6.1"
}

group "dev.architectury"

def runNumber = System.getenv("GITHUB_RUN_NUMBER") ?: "9999"

version = base_version + "." + runNumber

logger.lifecycle(":building architectury pack200 v${version}")

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}

repositories {
    maven { url "https://maven.architectury.dev/" }
    gradlePluginPortal()
    mavenCentral()
}

sourceSets {
    loomStub
    main.compileClasspath += loomStub.output
}

dependencies {
    implementation gradleApi()
}

gradlePlugin {
    plugins {
        architect {
            id = "dev.architectury.architectury-pack200"
            implementationClass = "dev.architectury.pack200.java.Pack200Plugin"
        }
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}
